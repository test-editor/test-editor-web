plugins {
    id "com.moowork.node" version "1.2.0"
}

node {
    version = '8.1.3'
    download = true
    yarnVersion = '1.7.0'
}

// clean node/node_modules/dist
task npmClean(type: Delete) {
    delete "$rootDir/node"
    delete "$rootDir/node_modules"
    delete "$rootDir/dist"
    delete "$rootDir/coverage"
}

task yarnInstall(type: YarnTask) {
    outputs.upToDateWhen { false }
    group = 'build'
    args = ['install']
}

task upgradeTesteditorDeps(type: YarnTask) {
  outputs.upToDateWhen { false }
  dependsOn(yarnInstall)
  group = 'build'
  args = ['upgrade', '-S', '@testeditor']
}

task bareBuild(type: NpmTask, dependsOn: 'upgradeTesteditorDeps') {
  group = 'build'
  description = 'Run ng build'
  args = ['run', 'bareBuild'] // corresponds with script @ package.json/scripts.bareBuild
}

task clean {
  dependsOn(npmClean)
}

task build(type: NpmTask, dependsOn: 'yarnInstall') {
    group = 'build'
    description = 'Run ng build'
    args = ['run', 'build'] // corresponds with script @ package.json/scripts.build
}

task start(type: NpmTask, dependsOn: 'yarnInstall') {
    group = 'run'
    description = 'Run ng serve (frontend on localhost:4200)'
    args = ['run', 'start'] // corresponds with script @ package.json/scripts.start
}

task testChrome(type: NpmTask, dependsOn: 'yarnInstall') {
    group = 'verification'
    description = 'Run ng test --browser ChromeHeadless --single-run'
    args = ['run', 'testCIChrome'] // corresponds with script @ package.json/scripts.testCIChrome
}

task testFirefox(type: NpmTask, dependsOn: 'yarnInstall') {
    group = 'verification'
    description = 'Run ng test --browser Firefox --single-run'
    args = ['run', 'testCIFirefox'] // corresponds with script @ package.json/scripts.testCIFirefox
}

task lint(type: NpmTask, dependsOn: 'yarnInstall') {
    group = 'verification'
    description = 'Run ng lint'
    args = ['run', 'lint'] // corresponds with script @ package.json/scripts.lint
}

task e2e(type: NpmTask, dependsOn: 'yarnInstall') {
    group = 'verification'
    description = 'Run ng e2e'
    args = ['run', 'e2e'] // corresponds with script @ package.json/scripts.e2e
}

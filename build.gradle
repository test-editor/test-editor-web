plugins {
  id "com.moowork.node" version "1.2.0"
}

node {
    version = '8.1.3'
    download = true
    nodeModulesDir = file("${project.projectDir}/")
}

// clean node/node_modules/dist
task npmClean(type: Delete) {
    final def webDir = "${rootDir}/frontend"
    delete "${webDir}/node"
    delete "${webDir}/node_modules"
    delete "${webDir}/dist"
    delete "${webDir}/coverage"
}

task clean {
    dependsOn(npmClean)
}

task build(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'build'
    description = 'Run ng build'
    args = ['run', 'build'] // corresponds with script @ package.json/scripts.build
}

task start(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'run'
    description = 'Run ng serve (frontend on localhost:4200)'
    args = ['run', 'start'] // corresponds with script @ package.json/scripts.start
}

task test(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'verification'
    description = 'Run ng test --browser Chrome --single-run'
    args = ['run', 'testCI'] // corresponds with script @ package.json/scripts.testCI
}

task lint(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'verification'
    description = 'Run ng lint'
    args = ['run', 'lint'] // corresponds with script @ package.json/scripts.lint
}

task e2e(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'verification'
    description = 'Run ng e2e'
    args = ['run', 'e2e'] // corresponds with script @ package.json/scripts.e2e
}
